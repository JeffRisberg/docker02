/**
 * @author Jeff Risberg
 * @since 11/26/17
 */

buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.1'
        classpath "com.avast.gradle:docker-compose-gradle-plugin:0.4.5"
    }
}

repositories {
    mavenCentral()
}

apply plugin: 'java'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'docker-compose'

version = '1.0'

def jarName = "docker02-all"

ext {
    slf4jVersion = '1.7.24'
}

assemble.dependsOn shadowJar


task buildApplicationDockerImage(type: Exec) {
    executable "docker"
    args "build", "-t", "incra/application", "-f", "Dockerfile.application", "."
}

task buildStorageDockerImage(type: Exec) {
    executable "docker"
    args "build", "-t", "incra/storage", "-f", "Dockerfile.storage", "."
}

task buildDockerImages(type: Exec) {
    executable "echo"
    args "Build images"
}
buildDockerImages.dependsOn(buildApplicationDockerImage)
buildDockerImages.dependsOn(buildStorageDockerImage)

task cleanApplicationDockerImage(type: Exec) {
    ignoreExitValue true
    executable "docker"
    args "rmi", "incra/application", "--force"
}

task cleanStorageDockerImage(type: Exec) {
    ignoreExitValue true
    executable "docker"
    args "rmi", "incra/storage", "--force"
}

task cleanDockerImages(type: Exec) {
    executable "echo"
    args "Cleaned images"
}
cleanDockerImages.finalizedBy(cleanApplicationDockerImage)
cleanDockerImages.finalizedBy(cleanStorageDockerImage)


dependencies {
    compile "org.slf4j:slf4j-api:$slf4jVersion"
    runtime "org.slf4j:slf4j-simple:$slf4jVersion"
    compile 'org.projectlombok:lombok:1.16.18'
    compile 'commons-collections:commons-collections:3.2.2'
    compile 'commons-configuration:commons-configuration:1.6'

    compile group: 'com.google.inject', name: 'guice', version: '4.1.0'
    compile group: 'com.google.inject.extensions', name: 'guice-servlet', version: '4.1.0'

    compile group: 'org.eclipse.jetty', name: 'jetty-server', version: '9.2.0.v20140526'
    compile group: 'org.eclipse.jetty', name: 'jetty-servlet', version: '9.2.0.v20140526'

    compile group: 'org.glassfish.jersey.core', name: 'jersey-common', version: '2.9.1'
    compile group: 'org.glassfish.jersey.core', name: 'jersey-server', version: '2.9.1'

    compile group: 'org.glassfish.jersey.containers', name: 'jersey-container-servlet', version: '2.9.1'
    compile group: 'org.glassfish.jersey.media', name: 'jersey-media-json-jackson', version: '2.9.1'
    compile group: 'org.glassfish.hk2', name: 'hk2', version: '2.3.0-b05'
    compile group: 'org.glassfish.hk2', name: 'guice-bridge', version: '2.2.0'

    compile group: 'org.hibernate.javax.persistence', name: 'hibernate-jpa-2.1-api', version: '1.0.0.Final'

    testCompile group: 'junit', name: 'junit', version: '4.+'
}


shadowJar {
    zip64 true
    baseName = jarName
    classifier = null
    mergeServiceFiles()
    manifest {
        attributes ('Implementation-Title': project.name,
                'Manifest-Version': '1.0',
                'Build-Jdk': '1.8.0_102')
    }
    from { [project.configurations.compile] } {
        exclude "META-INF/*.SF"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"
    }
    with jar
}
